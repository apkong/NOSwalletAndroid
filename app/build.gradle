repositories {
//    maven { url 'https://maven.fabric.io/public' }
}

apply plugin: 'com.android.application'
apply plugin: 'realm-android'
//apply plugin: 'io.fabric'


def currentVersionCode = 14
def currentVersionName = "0.0.14"

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "co.nos.noswallet"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode currentVersionCode
        versionName currentVersionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-fexceptions"
            }
        }
        // Enabling multidex support.
        multiDexEnabled true
        vectorDrawables.useSupportLibrary = true

        def empty = "\"\""

        buildConfigField "String", "CONNECTION_URL", (project.findProperty("CONNECTION_URL") ?: empty)
        buildConfigField "String", "WEBSOCKET_URL", (project.findProperty("WEBSOCKET_URL") ?: empty)

        //nollar representative
        buildConfigField "String", "REPRESENTATIVE", (project.findProperty("REPRESENTATIVE") ?: empty)

        buildConfigField "String", "NOS_REPRESENTATIVE_0", (project.findProperty("NOS_REPRESENTATIVE_0") ?: empty)
        buildConfigField "String", "NOS_REPRESENTATIVE_1", (project.findProperty("NOS_REPRESENTATIVE_1") ?: empty)
        buildConfigField "String", "NOS_REPRESENTATIVE_2", (project.findProperty("NOS_REPRESENTATIVE_2") ?: empty)

        buildConfigField "boolean", "DISABLE_SCREENSHOTS", (project.findProperty("DISABLE_SCREENSHOTS") ?: true)

//        buildConfigField "String", "CONNECTION_URL", "\"http://wb.cryptware.de:51234\""
    }
    buildTypes {
        debug {
            debuggable true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
        incremental false
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    final AWESOME_QRCODE_VERSION = '1.0.6'
    final EPOXY_VERSION = '2.12.0'
    final CONSTRAINT_LAYOUT_VERSION = '1.1.1'
    final DAGGER_VERSION = '2.14.1'
    final ESPRESSO_VERSION = '3.0.1'
    final FINGERPRINT_VERSION = '3.2.1'
    final GSON_FIRE_VERSION = '1.8.2'
    final GSON_VERSION = '2.8.2'
    final J_UNIT_VERSION = '4.12'
    final PINLOCKVIEW_VERSION = '2.1.0'
    final RX_ANDROID = '2.0.2'
    final RXBUS_VERSION = '2.0.0'
    final RX_JAVA_2 = '2.1.8'
    final SUPPORT_LIBRARY_VERSION = '27.1.1'
    final TEST_RUNNER_VERSION = '1.0.1'
    final TIMBER_VERSION = '4.7.0'
    final VAULT_VERSION = '1.4.0'
    final OKHTTP_VERSION = '3.10.0'
    final ZXING_VERSION = '1.9'

    implementation 'com.android.support:multidex:1.0.3'

    // Support
    implementation "com.android.support:appcompat-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:design:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:recyclerview-v7:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support:support-annotations:$SUPPORT_LIBRARY_VERSION"
    implementation "com.android.support.constraint:constraint-layout:$CONSTRAINT_LAYOUT_VERSION"

    // Dagger (dependency injection)
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    annotationProcessor "com.google.dagger:dagger-compiler:$DAGGER_VERSION"

    // Epoxy
    implementation "com.airbnb.android:epoxy:$EPOXY_VERSION"
    implementation "com.airbnb.android:epoxy-databinding:$EPOXY_VERSION"
    annotationProcessor "com.airbnb.android:epoxy-processor:$EPOXY_VERSION"

    // RX
    implementation "io.reactivex.rxjava2:rxjava:$RX_JAVA_2"
    implementation "io.reactivex.rxjava2:rxandroid:$RX_ANDROID"

    implementation "com.squareup.retrofit2:retrofit:2.4.0"
    implementation "com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0"
    implementation "com.squareup.retrofit2:converter-gson:2.4.0"

    // Bar Codes
    implementation "me.dm7.barcodescanner:zxing:$ZXING_VERSION"
    implementation "com.github.SumiMakito:AwesomeQRCode:$AWESOME_QRCODE_VERSION"

    // WebSocket
    implementation "com.squareup.okhttp3:okhttp:$OKHTTP_VERSION"

    implementation "com.squareup.okhttp3:logging-interceptor:3.10.0"
    implementation "com.facebook.stetho:stetho-okhttp3:1.5.0"

    // JSON Serialization
    implementation "com.google.code.gson:gson:$GSON_VERSION"
    implementation "io.gsonfire:gson-fire:$GSON_FIRE_VERSION"
    implementation "com.fasterxml.jackson.core:jackson-core:2.9.4"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.9.4"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.9.4"

    // Fingerprint
    implementation "com.github.ajalt.reprint:core:$FINGERPRINT_VERSION@aar"
    implementation "com.github.ajalt.reprint:reprint_spass:$FINGERPRINT_VERSION@aar"
    implementation "com.github.ajalt.reprint:rxjava2:$FINGERPRINT_VERSION@aar"

    // Pin
    implementation "com.andrognito.pinlockview:pinlockview:$PINLOCKVIEW_VERSION"

    // Event Bus
    implementation("com.hwangjr.rxbus:rxbus:$RXBUS_VERSION") {
        exclude group: 'com.jakewharton.timber', module: 'timber'
    }

    // Libsodium
    implementation project(':libsodium-jni-release')
    // custom built with forked libsodium to support blake2b hashing in ed25519 natively

    // Secure storage
    implementation "com.bottlerocketstudios:vault:$VAULT_VERSION"

    // Logging
    implementation "com.jakewharton.timber:timber:$TIMBER_VERSION"

    // Testing
    testAnnotationProcessor("com.google.dagger:dagger-compiler:$DAGGER_VERSION")
    androidTestAnnotationProcessor("com.google.dagger:dagger-compiler:$DAGGER_VERSION")
    androidTestImplementation "com.android.support.test:runner:$TEST_RUNNER_VERSION"
    androidTestImplementation("com.android.support.test.espresso:espresso-core:$ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$ESPRESSO_VERSION", {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude group: 'com.android.support', module: 'design'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude module: 'recyclerview-v7'
    })
    testImplementation "junit:junit:$J_UNIT_VERSION"
    testImplementation 'org.hamcrest:hamcrest-library:1.3'
    testImplementation 'org.mockito:mockito-core:2.8.9'

    //https://github.com/egslava/edittext-mask
    implementation 'ru.egslava:MaskedEditText:1.0.5'

    // https://mvnrepository.com/artifact/org.msgpack/msgpack
    implementation group: 'org.msgpack', name: 'msgpack', version: '0.6.12'

    implementation 'com.google.firebase:firebase-core:16.0.1'

    implementation 'com.google.firebase:firebase-messaging:17.3.4'

//    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
//        transitive = true
//    }
//    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
//        transitive = true
//    }
}

apply plugin: 'com.google.gms.google-services'